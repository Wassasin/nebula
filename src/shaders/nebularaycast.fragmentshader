#version 130

in vec4 frag_vertex;

uniform sampler2D tex;
uniform sampler3D volume_tex;

uniform mat4 mvp;
uniform float stepsize;

void main(void)
{
	vec4 front_position = mvp * frag_vertex;

	vec2 texc = ((front_position.xy / front_position.w) + 1) / 2;	// find the right place to lookup in the backside buffer
	vec4 back_position = texture2D(tex, texc);
	vec3 dir = (back_position - frag_vertex).xyz;
	
	float len = length(dir.xyz);
	vec3 norm_dir = normalize(dir);
	vec3 delta_dir = norm_dir * stepsize;
	float delta_dir_len = length(delta_dir);
	
	vec3 vec = frag_vertex.xyz;
	vec4 col_acc = vec4(0, 0, 0, 0);
	float alpha_acc = 0;
	float length_acc = 0;
	
	vec4 color_sample;
	float alpha_sample;
	for(int i = 0; i < 450; ++i)
	{
		color_sample = texture3D(volume_tex, vec);
		alpha_sample = color_sample.a * stepsize;
		
		col_acc += (1.0 - alpha_acc) * color_sample * alpha_sample * 3;
		alpha_acc += alpha_sample;
		vec += delta_dir;
		length_acc += delta_dir_len;
		
		if(length_acc >= len || alpha_acc > 1.0)
			break; // Terminate if opacity > 1, or the ray is outside the volume
	}
	
	gl_FragColor = col_acc;
}
